#!/usr/bin/env nu

let history_dir = (if "$XDG_DATA_HOME" in $env { $env.XDG_DATA_HOME } else { $env.HOME | path join ".local/share" }) | path join "rofi-finder"
let history_file = $history_dir | path join "history"

def read_history [history_file: path]: nothing -> list {
  mkdir $history_dir

  if ($history_file | path exists) {
    open $history_file -r | lines
  } else {
    []
  }
}

def update_history [path: string]: list -> list {
  $in | | prepend $path | uniq | first 100
}

def write_history [history_file: path]: list -> nothing {
  $in | save -f $history_file
}


def parse_type [ type: string ]: nothing -> closure {
  if ($type == "file") {
    { $in.type == "file" }
  } else if ($type == "dir") {
    { $in.type == "dir" }
  } else if ($type == "") {
    { true }
  } else {
    error make {msg: $"Invalid type ($type)"}
  }
}

def get_results [dir: path, type_cond: closure]: nothing -> table {
  ls **/* | where {do $type_cond }
}


def main [
  --stdout (-o)
  --type (-t): string = ""
  --mult (-m)
  dir: path = "."
] {

  # switch to the dir because globs are not expanded with vars
  cd $dir
  let dir = pwd

  let rofi_mult = if $mult { "-multi-select" } else { "" } 
  
  let history = read_history $history_file
    # history has full paths; we're trimming them
    | where { str starts-with (pwd) } | str replace ((pwd) + "/") ""
    | where { do (parse_type $type) }
  
  
  let output = get_results $dir (parse_type $type) | get "name"
    | where { not ($in in $history) } | prepend $history
    | to text
    | rofi -dmenu -i $rofi_mult -kb-custom-1 "Alt+n" -kb-custom-2 "Alt+c" -kb-custom-3 "Alt+e" -p "open"
    | complete

  let pick = $output.stdout
  
  match $output.exit_code {
    0 => (if $stdout {
          $dir | path join $pick | print
        } else {
          $pick | xargs | xdg-open $in # idk what's the problem but xargs fixes it
        }),
    10 => (main --type=($type) --mult=($mult) $pick)
    11 => (copy-file $pick)
  }

  if $output.exit_code in [0, 11] {
    read_history $history_file | update_history (pwd | path join $pick) | write_history $history_file
  }
}
